plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "vsu.csf.procat"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 4
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            File signFile = rootProject.file('debug_keystore.properties')
            Properties props = new Properties()
            props.load(new FileInputStream(signFile))
            storeFile file(props['keystore'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
        release {
            File signFile = rootProject.file('release_keystore.properties')
            Properties props = new Properties()
            props.load(new FileInputStream(signFile))
            storeFile file(props['keystore'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            buildConfigField "String", "SERVER_URL", "\"http://www.test.ru\""

            debuggable true
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.release

            buildConfigField "String", "SERVER_URL", "\"https://www.test.ru\""

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {

    implementation project(':network')

    implementation 'androidx.core:core-ktx:1.5.0'
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation 'androidx.appcompat:appcompat:1.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"

    implementation 'com.google.android.material:material:1.4.0-rc01'

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.10'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // Dagger
    ext.dagger_version = '2.35.1'
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"

    // Room:
    ext.room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"

    // Glide:
    ext.glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Input Mask
    implementation 'com.github.RedMadRobot:input-mask-android:6.0.0'

    // Barcode Scanner
    implementation 'com.google.mlkit:barcode-scanning:16.1.2'

    // CameraX dependencies
    implementation "androidx.camera:camera-camera2:1.0.0"
    implementation "androidx.camera:camera-lifecycle:1.0.0"
    implementation "androidx.camera:camera-view:1.0.0-alpha25"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.10.4"
    testImplementation "com.google.truth:truth:1.1"

}